let s:cwd = getcwd() . '/test'
let s:assert = themis#helper('assert')

function! s:exec(command)
  if exists('*execute')
    " Vim 7.4.2008 and later
    return execute(a:command)
  elseif exists('*evalcmd')
    " Vim 7.4.1996 to .2007
    return evalcmd(a:command)
  else
    redir => l:cmdoutput
    execute a:command
    redir END
    return l:cmdoutput
  endif
endfunction

describe init.vim
  describe auto-lcd
    it is okay with spaces in the file path
      execute printf("edit %s/src/path\\ space/blah.c", s:cwd)
      call s:assert.equals(getcwd(), printf("%s/src/path\ space", s:cwd))
    end
  end

  describe listchars
    it calls out tabs
      call s:assert.match(&listchars, 'tab:. ')
      call s:assert.equals(&list, 1, "'list' not set")
    end

    it highlights tabs differently from the normal background
      let l:normal_hi_text = s:exec('highlight Normal')
      let l:normal_bg = substitute(l:normal_hi_text, '.*ctermbg=\(\d\+\).*', '\1', '')
      let l:ws_hi_text = s:exec('highlight Whitespace')
      " Sanity check.
      call s:assert.match(l:ws_hi_text, 'ctermbg=\(\d\+\)', "Whitespace does not override ctermbg")
      let l:ws_bg = substitute(l:ws_hi_text, '.*ctermbg=\(\d\+\).*', '\1', '')

      call s:assert.not_equals(l:normal_bg, l:ws_bg, "Whitespace background should not be Normal background")
    end
  end
end
