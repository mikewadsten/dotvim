" Assumes cwd is dotvim/ but that should be good enough
let s:cwd = getcwd() . '/test'
let s:assert = themis#helper('assert')

function! s:check_tag()
  call s:assert.not_match(bufname('%'), 'foo.c$')
  tag myfunc
  call s:assert.match(bufname('%'), 'foo.c$')

  bdelete!
endfunction

describe cscope
  before all
    call system(printf("rm -f %s/cscope.*out*", s:cwd))
    call system(printf("rm -f %s/GPATH %s/GTAGS %s/GRTAGS", s:cwd, s:cwd, s:cwd))

    cscope kill -1
    execute "chdir " . s:cwd
    call system("cscope -Rbq -P" . s:cwd)
    source ../cscope.vim
    chdir -
  end

  after all
    call system(printf("rm -f %s/cscope.*out*", s:cwd))
    call system(printf("rm -f %s/GPATH %s/GTAGS %s/GRTAGS", s:cwd, s:cwd, s:cwd))
  end

  before each
    cscope kill -1
    chdir /tmp
    call s:assert.equals(cscope_connection(), 0, "Saw cscope connection after chdir /tmp")
  end

  describe cscope#detect()
    it works with directory argument
      let b:git_dir = '/tmp'
      let retval = cscope#detect(s:cwd)
      call s:assert.equals(retval, 1, "Bad entry state")
      call s:assert.equals(cscope_connection(), 1, "Didn't see cscope connection after detect")
      call s:assert.equals(cscope_connection(2, s:cwd . '/cscope.out'), 1,
            \              "Didn't find cscope.out in given dir!")

      call s:check_tag()
    end

    it uses b:git_dir when called without argument
      let b:git_dir = s:cwd . '/.git'
      let retval = cscope#detect()
      call s:assert.equals(retval, 1, "Bad entry state")
      call s:assert.equals(cscope_connection(), 1,
            \              "Did not see cscope connection after detect")
      call s:assert.equals(cscope_connection(2, s:cwd . '/cscope.out'), 1,
            \              "Didn't find cscope.out in given dir!")

      call s:check_tag()
    end
  end

  describe CscopeAdd command
    it uses file directory as prepath if not given
      execute "CscopeAdd " . s:cwd . "/cscope.out"

      call s:assert.equals(cscope_connection(), 1)
      " Check the file and prepath are correct
      call s:assert.equals(cscope_connection(4, s:cwd . '/cscope.out', s:cwd), 1)

      call s:check_tag()
    end

    it uses prepath argument if given
      execute "CscopeAdd " . s:cwd . "/cscope.out /tmp"

      call s:assert.equals(cscope_connection(), 1)
      " Check the file and prepath are correct
      call s:assert.equals(cscope_connection(4, s:cwd . '/cscope.out', '/tmp'), 1)
    end

    it accepts flags argument if given
      execute "CscopeAdd " . s:cwd . "/cscope.out /var -C"

      call s:assert.equals(cscope_connection(), 1)
      " Check the file and prepath are correct. Can't inspect flags though :(
      call s:assert.equals(cscope_connection(4, s:cwd . '/cscope.out', '/var'), 1)
    end

    it treats prepath as flags if it starts with -
      " prepath should be file's directory, as in a test above.
      execute "CscopeAdd " . s:cwd . "/cscope.out -C"

      call s:assert.equals(cscope_connection(), 1)
      " Check the file and prepath are correct. Can't inspect flags though :(
      call s:assert.equals(cscope_connection(4, s:cwd . '/cscope.out', s:cwd), 1)

      call s:check_tag()
    end
  end

  describe GtagsAdd command
    before all
      call system(printf("rm -f %s/cscope.*out", s:cwd))
      execute "chdir " . s:cwd
      call system("gtags --gtagslabel ctags")
      chdir -
    end

    before each
      cscope kill -1
    end

    it uses file directory as prepath if not given
      execute "GtagsAdd " . s:cwd . "/GTAGS"

      call s:assert.equals(cscope_connection(), 1, "No cscope connection after GtagsAdd")
      call s:assert.equals(cscope_connection(4, s:cwd . '/GTAGS', s:cwd), 1, "No matching cscope connection after GtagsAdd")

      call s:check_tag()
    end

    it uses prepath argument if given
      execute "GtagsAdd " . s:cwd . "/GTAGS /tmp"

      call s:assert.equals(cscope_connection(), 1, "No cscope connection after GtagsAdd")
      call s:assert.equals(cscope_connection(4, s:cwd . '/GTAGS', '/tmp'), 1, "No matching cscope connection after GtagsAdd")
    end

    it accepts flags argument if given
      execute "GtagsAdd " . s:cwd . "/GTAGS /var -C"

      call s:assert.equals(cscope_connection(), 1, "No cscope connection after GtagsAdd")
      call s:assert.equals(cscope_connection(4, s:cwd . '/GTAGS', '/var'), 1, "No matching cscope connection after GtagsAdd")
    end

    it treats prepath as flags if it starts with -
      execute "GtagsAdd " . s:cwd . "/GTAGS -iaC"

      call s:assert.equals(cscope_connection(), 1, "No cscope connection after GtagsAdd")
      call s:assert.equals(cscope_connection(4, s:cwd . "/GTAGS", s:cwd), 1, "No matching cscope connection after GtagsAdd")

      call s:check_tag()
    end
  end
end

describe cscope#gen...
  before all
    execute printf("chdir %s", s:cwd)
    source ../cscope.vim
    chdir -
  end

  after all
    call system(printf('rm -f %s/cscope*.out*', s:cwd))
    call system(printf('rm -f %s/GPATH %s/GTAGS %s/GRTAGS', s:cwd, s:cwd, s:cwd))
  end

  before each
    call system(printf('rm -f %s/cscope*.out*', s:cwd))
    call system(printf('rm -f %s/GPATH %s/GTAGS %s/GRTAGS', s:cwd, s:cwd, s:cwd))

    cscope kill -1
    chdir /tmp

    unlet! b:git_dir
  end

  describe #gen_cscope
    before each
      call system(printf('rm -f %s/src/cscope*.out*', s:cwd))
    end

    after each
      call system(printf('rm -f %s/src/cscope*.out*', s:cwd))
    end

    it works with directory argument
      call s:assert.false(filereadable(printf("%s/src/cscope.out", s:cwd)))
      let retval = cscope#gen_cscope(s:cwd . '/src')

      call s:assert.equals(retval, 1)
      call s:assert.true(filereadable(printf("%s/src/cscope.out", s:cwd)))

      " However, it doesn't add a connection automatically.
      call s:assert.equals(cscope_connection(), 0, "Saw connection after")
    end

    it uses b:git_dir when called without argument
      call s:assert.false(filereadable(printf("%s/src/cscope.out", s:cwd)))

      let b:git_dir = printf('%s/src/.git', s:cwd)
      let retval = cscope#gen_cscope()

      call s:assert.equals(retval, 1)
      call s:assert.true(filereadable(printf("%s/src/cscope.out", s:cwd)))

      " However, it doesn't add a connection automatically.
      call s:assert.equals(cscope_connection(), 0, "Saw connection after")
    end

    " Funny name, but I put it here to reuse before/after logic
    it is called by CscopeGen and then we detect it
      call s:assert.false(filereadable(printf("%s/src/cscope.out", s:cwd)))
      execute printf("CscopeGen %s/src", s:cwd)

      call s:assert.true(filereadable(printf("%s/src/cscope.out", s:cwd)))

      call s:assert.equals(cscope_connection(2, s:cwd.  "/src/cscope.out"), 1,
            \              "Did not automatically connect to new database")
    end
  end

  describe #gen_gtags
    before each
      call system(printf('rm -f %s/src/GPATH %s/src/GTAGS %s/src/GRTAGS', s:cwd, s:cwd, s:cwd))
    end

    after each
      call system(printf('rm -f %s/src/GPATH %s/src/GTAGS %s/src/GRTAGS', s:cwd, s:cwd, s:cwd))
    end

    it works with directory argument
      call s:assert.false(filereadable(printf("%s/src/GTAGS", s:cwd)))
      let retval = cscope#gen_gtags(s:cwd . '/src')

      call s:assert.equals(retval, 1)
      call s:assert.true(filereadable(printf("%s/src/GTAGS", s:cwd)))

      " However, it doesn't add a connection automatically.
      call s:assert.equals(cscope_connection(), 0, "Saw connection after")
    end

    it uses b:git_dir when called without argument
      call s:assert.false(filereadable(printf("%s/src/GTAGS", s:cwd)))

      let b:git_dir = printf('%s/src/.git', s:cwd)
      let retval = cscope#gen_gtags()

      call s:assert.equals(retval, 1)
      call s:assert.true(filereadable(printf("%s/src/GTAGS", s:cwd)))

      " However, it doesn't add a connection automatically.
      call s:assert.equals(cscope_connection(), 0, "Saw connection after")
    end

    " Funny name, but I put it here to reuse before/after logic
    it is called by GtagsGen and then we detect it
      call s:assert.false(filereadable(printf("%s/src/GTAGS", s:cwd)))
      execute printf("GtagsGen %s/src", s:cwd)

      call s:assert.true(filereadable(printf("%s/src/GTAGS", s:cwd)))

      call s:assert.equals(cscope_connection(2, s:cwd.  "/src/GTAGS"), 1,
            \              "Did not automatically connect to new database")
    end
  end
end
